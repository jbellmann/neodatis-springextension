apply plugin: 'maven'

optionalDeps = []
providedDeps = []

optional = { optionalDeps << it }
provided = { providedDeps << it }

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = 'https://github.com/jbellmann/neodatis-springextension'
			organization {
				name = 'Joerg Bellmann'
				url = 'https://github.com/jbellmann/'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = 'https://github.com/jbellmann/neodatis-springextension'
				connection = 'scm:git:https://github.com/jbellmann/neodatis-springextension'
				developerConnection = 'scm:git:https://github.com/jbellmann/neodatis-springextension'
			}
			developers {
				developer {
					id = 'jbellmann'
					name = 'Joerg Bellmann'
					email = 'joerg.bellmann@googlemail.com'
				}
			}
		}
	}
}